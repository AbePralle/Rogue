class Texture
  PROPERTIES
    id     : Integer

    image_width   : Integer
    image_height  : Integer

    texture_width  : Integer  # in texels
    texture_height : Integer  # in texels

    bits_per_pixel : Integer  # always 32 at the moment

    reference_count : Integer

  METHODS
    method init( bitmap:Bitmap, flags=0:Integer )
      id = Graphics.create_texture( bitmap.width, bitmap.height, bitmap.pixels, flags )
      update_info

    method init( filepath:String, flags=0:Integer )
      id = Graphics.load_texture_from_png( filepath, flags )
      update_info

    method image_size->XY
      return XY(image_width,image_height)

    method destroy
      Graphics.native_destroy_texture( id )
      id = 0

    method set( new_data:Bitmap, flags=0:Integer )
      if (id?)
        Graphics.set_texture( id, new_data.width, new_data.height, new_data.pixels, flags )
      else
        id = Graphics.create_texture( new_data.width, new_data.height, new_data.pixels, flags )
      endIf
      update_info

    method texture_size->XY
      return XY(texture_width,texture_height)

    method update_info
      if (not id) return

      local info = Graphics.native_get_texture_info( id )
      image_width = info.image_width
      image_height = info.image_height
      texture_width = info.texture_width
      texture_height = info.texture_height
      bits_per_pixel = info.bits_per_pixel
endClass

compound TextureInfo( image_width:Integer, image_height:Integer, texture_width:Integer, texture_height:Integer, bits_per_pixel:Integer );

