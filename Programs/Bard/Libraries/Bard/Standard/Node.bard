#==============================================================================
# BardNode
#==============================================================================
aspect BardNode<<$OfType>>
  PROPERTIES
    parent    : $OfType
    child     : $OfType
    next      : $OfType

  METHODS
    method add( new_child:$OfType )->$OfType
      new_child.detach.parent = this

      if (child?)
        local cur = child
        while (cur.next?) cur = cur.next
        cur.next = new_child
      else
        child = new_child
      endIf
      return this

    method detach->$OfType
      if (parent?)
        if (parent.child is this)
          parent.child = this.next
        else
          local cur = parent.child
          while (cur.next isNot this) cur = cur.next
          cur.next = this.next
        endIf
        parent = null
      endIf
      next = null
      return this

    method detach_children
      while (child?) child.detach

    method insert( new_child:$OfType )->$OfType
      new_child.detach.parent = this
      new_child.next = this.child
      this.child = new_child
      return this

    method last_child->$OfType
      local cur = child
      while (cur.next?) cur .= next
      return cur

endAspect

