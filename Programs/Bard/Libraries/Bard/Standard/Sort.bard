class Sort<<$DataType>>
  METHODS
    method bubble_sort( list:$DataType[], fn:Delegate($DataType,$DataType)->Logical )
      forEach (pass in 1..list.count)
        local sorted = true
        forEach (i in 1..(list.count-pass))
          local a = list[i-1]
          local b = list[i]
          if (not fn.call(a,b))
            sorted = false
            list[i-1] = b
            list[i]   = a
          endIf
        endForEach
        if (sorted) return
      endForEach

    method heapsort( list:$DataType[], fn:Delegate($DataType,$DataType)->Logical )
      # Sorts 'list' in-place using heapsort.

      # Heapify
      forEach (index of list)
        local cur = index
        local parent = (index - 1) / 2
        while (cur != 0 and not fn.call(list[cur],list[parent]))
          list.swap( cur, parent )
          cur = parent
          parent = (parent - 1) / 2
        endWhile
      endForEach

      # sort
      local index = list.count - 1
      while (index > 0)
        list.swap( 0, index )

        # push value at heap top back down
        local cur = 0
        loop
          local left_child = cur * 2 + 1
          local right_child = cur * 2 + 2
          local cur_value = list[cur]
          if (right_child < index)
            # two children to pick from
            local left_value = list[left_child]
            local right_value = list[right_child]
            if (fn.call(cur_value,left_value) and not fn.call(left_value,right_value))
              list.swap( cur, left_child )
              cur = left_child
            elseIf (fn.call(cur_value,right_value))
              list.swap( cur, right_child )
              cur = right_child
            else
              escapeLoop
            endIf
          elseIf (left_child < index and fn.call(cur_value,list[left_child]))
            # only the left child is available - also means this
            # is last iteration for pushing
            list.swap( cur, left_child )
            escapeLoop
          else
            escapeLoop
          endIf
        endLoop

        --index
      endWhile

    method quicksort( list:$DataType[], fn:Delegate($DataType,$DataType)->Logical )
      quicksort( list.data, fn, 0, list.count-1 )

    method quicksort( data:Array<<$DataType>>, fn:Delegate($DataType,$DataType)->Logical, i1:Integer, i2:Integer )
      if (i1 >= i2)
        # Zero or one elements - already sorted
        return

      elseIf (i1 + 1 == i2)
        # Optimization for two elements
        local a = data[i1]
        local b = data[i2]
        if (not fn.call(a,b))
          data[i2] = a
          data[i1] = b
        endIf
        return

      elseIf (i1 + 2 == i2)
        # Optimization for three elements
        local ib = i1 + 1
        local a = data[i1]
        local b = data[ib]
        local c = data[i2]
        if (fn.call(a,b))
          # abc, acb, cab
          if (fn.call(b,c))
            # abc
            return
          elseIf (fn.call(a,c))
            # acb
            data[ib] = c
            data[i2] = b
            return
          else
            # cab
            data[i1] = c
            data[ib] = a
            data[i2] = b
            return
          endIf
        else
          # bac, bca, cba
          if (fn.call(a,c))
            # bac
            data[i1] = b
            data[ib] = a
            return
          elseIf (fn.call(b,c))
            # bca
            data[i1] = b
            data[ib] = c
            data[i2] = a
            return
          else
            # cba
            data[i1] = c
            data[ib] = b
            data[i2] = a
            return
          endIf
        endIf
      endIf

      # General case - 4 or more elements.  Select the median value
      # of the first, middle, and last numbers as the pivot.
      local pivot_index = (i1+i2) :>>: 1
      local pivot = data[ pivot_index ]
      local first = data[i1]
      if (fn.call(pivot,first)) pivot = first; pivot_index = i1
      local last = data[i2]
      if (fn.call(last,pivot)) pivot = last; pivot_index = i2

      # Swap numbers until the left part of the array contains
      # everything less than the pivot value and the right part contains
      # everything greater than the pivot value - these two parts
      # may well differ in size. 

      # Place the pivot at the end temporarily
      local temp = data[pivot_index]
      data[pivot_index] = data[i2]
      data[i2] = temp

      pivot_index = i1
      forEach (i in i1..<i2)
        if (fn.call(data[i],pivot))
          # Read along swapping values when necessary so that all values 
          # ordered before the pivot are on one side.
          temp = data[i]
          data[i] = data[pivot_index]
          data[pivot_index] = temp
          ++pivot_index
        endIf
      endForEach

      # Put the pivot back in the middle
      local temp = data[pivot_index]
      data[pivot_index] = data[i2]
      data[i2] = temp

      quicksort( data, fn, i1, pivot_index-1 )
      quicksort( data, fn, pivot_index+1, i2 )

endClass
