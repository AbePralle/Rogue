$[include "Local.bard"]

class Method
  PROPERTIES
    t            : Token
    type_context : Type
    name         : String
    signature    : String
    cpp_name     : String
    attributes   : Integer

    return_type  : Type

    parameters = Local[] : Local[]
    locals     = Local[] : Local[]

    statements = CmdStatementList()

    generic_method : Method
    native_code    : String

    resolved  : Logical

  METHODS
    method init( t, type_context, name )

    method accepts_arg_count( n:Integer )->Logical
      return n == parameters.count

    method add_local( v_t:Token, v_name:String )->Local
      local v = Local( v_t, v_name )
      v.index = locals.count
      locals.add( v )
      return v

    method add_parameter( p_t:Token, p_name:String )->Local
      local p = add_local( p_t, p_name )
      parameters.add( p )
      return p

    method assign_cpp_name
      local buffer = StringBuilder()
      buffer.print( Program.code_prefix )
      buffer.print( type_context.name ).print( "__" )
      buffer.print( name )
      cpp_name = Program.validate_cpp_name( buffer->String )

    method assign_signature
      local buffer = StringBuilder()
      buffer.print name
      buffer.print "("
      local first = true
      forEach (param in parameters)
        if (first) first = false
        else       buffer.print( "," )
        buffer.print( param.type.name )
      endForEach
      buffer.print ")"
      signature = buffer->String

    method compile_target->Method
      if (generic_method?) return generic_method
      return this

    method is_initializer->Logical
      return (attributes & Attribute.is_initializer)?

    method is_native->Logical
      return (attributes & Attribute.is_native)?

    method organize->Method
      if (name == "init")
        attributes |= Attribute.is_initializer
        return_type = type_context
      endIf

      forEach (param in parameters)
        local property = type_context.find_property( param.name )

        if (property?)
          if (param.type is null)
            # Autostore parameter
            local new_name = Program.create_unique_id
            local pt = property.t
            statements.insert( CmdAssign(pt,CmdAccess(pt,property.name),CmdAccess(pt,new_name)) )
            param.name = new_name
          else
            throw param.t.error( //A property named "$" already exists.// (param.name) )
          endIf
        endIf

        if (not param.type) throw param.t.error( //No type declared for parameter "$".// (param.name) )
        param.type.resolve
      endForEach

      if (return_type?) return_type.resolve

      assign_signature

      # Add method to type's lookup table.
      local existing_m = type_context.method_lookup_by_signature[signature]
      if (existing_m?)
        # We haven't inherited methods yet so this is a plain old duplicate
        throw t.error( "A method with the signature $ is already defined." (signature) )
      endIf
      type_context.method_lookup_by_signature[signature] = this

      return this

    method print_prototype( writer:CPPWriter )
      if (is_native) return
      print_signature( writer )
      writer.println( ";" )

    method print_signature( writer:CPPWriter )
      writer.print( return_type ).print(" ").print( cpp_name )
      writer.print( "( " )
      writer.print( type_context )
      writer.print( " THIS" )
      forEach (param in parameters)
        writer.print( ", " ).print( param.type ).print( " " ).print( param.cpp_name )
      endForEach
      writer.print( " )" )

    method print_definition( writer:CPPWriter )
      if (is_native) return
      print_signature( writer )
      writer.println
      writer.println "{"
      writer.indent += 2
      statements.print_cpp( writer )
      writer.indent -= 2
      writer.println "}"
      writer.println


    method resolve
      if (resolved) return
      resolved = true

      if (type_context.generic_type?)
        # Find the generic version of this method
        type_context.generic_type.resolve
        local buffer = StringBuilder()
        buffer.print( name ).print( "(" )

        local first = true
        forEach (p in parameters)
          if (first) first = false
          else       buffer.print( "," )
          if (p.generic_type?) buffer.print( p.generic_type.name )
          else                 buffer.print( p.type.name )
        endForEach

        buffer.print( ")" )

        local generic_sig = buffer->String
        if (generic_sig != signature)
          generic_method = type_context.generic_type.find_method( generic_sig )
          if (not generic_method)
            throw t.error( "[INTERNAL] Could not find generic mapping of $::$ -> $::$" (type_context,signature,type_context.generic_type,generic_sig) )
          endIf
        endIf
      endIf

      local scope = Scope( type_context, this )
      forEach (p in parameters) scope.push_local( p )

      statements.resolve( scope )

      if (is_initializer)
        local return_t = t
        if (statements.count?) return_t = statements.last.t
        statements.add( CmdReturn(return_t,CmdReadThis(return_t,type_context)).resolve(scope) )
      endIf

    method to->String
      return signature
endClass

