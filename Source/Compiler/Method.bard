$[include "Local.bard"]

class Method
  PROPERTIES
    t           : Token
    this_type   : Type
    name        : String
    signature   : String
    cpp_name    : String
    attributes  : Integer

    return_type : Type

    locals = Local[] : Local[]
    statements = CmdStatementList()

    resolved  : Logical

  METHODS
    method init( t, this_type, name )

    method add_local( v_t:Token, v_name:String )->Local
      local v = Local( v_t, v_name )
      v.index = locals.count
      locals.add( v )
      return v

    method assign_cpp_name
      local buffer = StringBuilder()
      buffer.print( Program.code_prefix )
      buffer.print( this_type.name ).print( "__" )
      buffer.print( name )
      cpp_name = Program.validate_cpp_name( buffer->String )

    method assign_signature
      local buffer = StringBuilder()
      buffer.print name
      buffer.print "("
      buffer.print ")"
      signature = buffer->String

      this_type.method_lookup_by_signature[signature] = this

    method is_initializer->Logical
      return (attributes & Attributes.flag_initializer)?

    method organize
      if (name == "init")
        attributes |= Attributes.flag_initializer
        return_type = this_type
      endIf

      assign_signature

    method print_prototype( writer:CPPWriter )
      print_signature( writer )
      writer.println( ";" )

    method print_signature( writer:CPPWriter )
      writer.print( return_type ).print(" ").print( cpp_name )
      writer.print( "( " )
      writer.print( this_type )
      writer.print( " THIS" )
      writer.print( " )" )

    method print_definition( writer:CPPWriter )
      print_signature( writer )
      writer.println
      writer.println "{"
      writer.indent += 2
      statements.print_cpp( writer )
      writer.indent -= 2
      writer.println "}"
      writer.println


    method resolve
      if (resolved) return
      resolved = true

      local scope = Scope( this_type, this )
      statements.resolve( scope )

      if (is_initializer)
        local return_t = t
        if (statements.count?) return_t = statements.last.t
        statements.add( CmdReturnValue(return_t,CmdReadThis(return_t,this_type)).resolve(scope) )
      endIf
endClass

