class Method
  PROPERTIES
    t           : Token
    this_type   : Type
    name        : String
    cpp_name    : String
    attributes  : Integer

    return_type : Type

    statements = CmdStatementList()

    resolved  : Logical

  METHODS
    method init( t, this_type, name )

    method assign_cpp_name
      local buffer = StringBuilder()
      buffer.print( Program.code_prefix )
      buffer.print( this_type.name ).print( "__" )
      buffer.print( name )
      cpp_name = Program.validate_cpp_name( buffer->String )

    method is_initializer->Logical
      return (attributes & flag_initializer)?

    method print_prototype( writer:CPPWriter )
      print_signature( writer )
      writer.println( ";" )

    method print_signature( writer:CPPWriter )
      writer.print( return_type ).print(" ").print( cpp_name )
      writer.print( "(" )
      writer.print( ")" )

    method resolve
      if (resolved) return
      resolved = true

      if (name == "init")
        attributes |= Attributes.flag_initializer
        return_type = this_type
      endIf

      statements.resolve
endClass

