class Templates : Table<<String,Template>> [singleton]
  METHODS
    method instantiate( ref_t:Token, def:Type )
      local name = def.name
      local dims = 0

      local temp = this[ name ]
      if (temp?)
        if (temp.placeholder_names.count?)
          # TODO: figure out logic here
          throw ref_t.error( name + " is a template; specializing types expected.  Example: TypeName<<String,Integer>>." )
        endIf
      else
        if (name.ends_with(']'))
          while (name.ends_with(']')) name = name.leftmost(-2); ++dims
          temp = this[ "[]" ]

        elseIf (name.ends_with('>'))
          local base_name = name.before_first('<')
          temp = this[ base_name ]
          if (temp? and temp.placeholder_names.count == 0)
            throw ref_t.error( base_name + " is not a template; no specializing types in <<angle brackets>> should be specified." )
          endIf
        endIf
      endIf

      if (temp is null)
        local t = def.t
        if (ref_t?) t = def.t
        throw t.error( "Reference to undefined type '" + name + "'." )
      endIf

      if (dims?)
        temp.instantiate_list( def, name, dims )
      elseIf (temp.placeholder_names.count?)
        if (temp.is_generic)
          temp.instantiate_generic_template( ref_t, def )
        else
          temp.instantiate_template( ref_t, def )
        endIf
      else
        temp.instantiate( def )
      endIf
endClass

class Template
  PROPERTIES
    t                    : Token
    name                 : String
    tokens               = Token[]
    placeholder_names    = String[]
    generic_type_names   = String[]
    is_generic           : Logical

    attributes           = Attributes()

    generic_map          : Type
      # A type with properties and empty-body methods that was parsed with the placeholder
      # name in-place, making it easy to substitute specializer names and generic type names
      # in order to easily map properties and methods to the corresponding parts in the 
      # generic base type.

  METHODS
    method init( t, name, flags:Integer )
      attributes.add( flags )

    method create_generic_map
      if (generic_map?) return

      # Generate the generic map type.
      local map = Table<<String,TokenList>>()
      local buffer = StringBuilder()

      if (name.begins_with('['))
        local placeholder_name = placeholder_names.first
        map[ placeholder_name ] = TokenList( IdentifierToken(t,placeholder_name) )
        buffer.print( placeholder_name )
        buffer.print( "[]" )
      else
        buffer.print( name )
        buffer.print( "<<" )

        local i = 0
        while (i < generic_type_names.count)
          if (i > 0) buffer.print(',')
          local placeholder_name = placeholder_names[i]
          map[ placeholder_name ] = TokenList( IdentifierToken(t,placeholder_name) )
          buffer.print( placeholder_name )
          ++i
        endWhile
        buffer.print( ">>" )
      endIf

      generic_map = Types.reference( t, buffer->String )
      instantiate_template( generic_map, map )
      generic_map.is_defined = true

      # Clear bodies of generic methods
      forEach (m in generic_map.method_list)
        if (m.type_context is generic_map)
          m.body.clear
        endIf
      endForEach

    method add_augment_tokens( type:Type, instance_tokens:Token[] )
      local instance_name = type.name

      local augment_list = Types.augments[instance_name]
      if (augment_list?)
        local last_t = instance_tokens.remove_last  # endClass/endPrimitive etc
        forEach (aug in augment_list)
          instance_tokens.add( aug.tokens )
          type.base_types.add( aug.base_types )
        endForEach
        instance_tokens.add( last_t )
        Types.augments[instance_name] = null
      endIf

    method instantiate( def:Type )
      def.attributes.add( attributes )
      add_augment_tokens( def, tokens )
      Parser().parse_type_def( def, tokens )

    method instantiate_template( ref_t:Token, def:Type )
      local map = Table<<String,TokenList>>()

      local parser = Parser( def.t.filepath, def.name.after_first("<<") )

      local index = 0
      local first = true
      while (first or parser.consume(Token.symbol_comma))
        first = false

        if (index > placeholder_names.count)
          throw ref_t.error( "Too many substitution types specified; expected " + placeholder_names.count + "." )
        endIf
        local replacement_tokens = TokenList()
        while (not (parser.next_is(Token.symbol_comma) or parser.next_is(Token.symbol_close_specialize)))
          if (not parser.reader.has_another) throw ref_t.error( "Missing closing '>>'." )
          local cur_t = parser.read
          #if (cur_t.type != Token.symbol_backslash) 
          replacement_tokens.add( cur_t )
        endWhile
        map[ placeholder_names[index] ] = replacement_tokens

        ++index
      endWhile
      parser.must_consume( Token.symbol_close_specialize )

      if (index < placeholder_names.count)
        throw ref_t.error( "Not enough substitution types specified; expected " + (placeholder_names.count) + "." )
      endIf

      instantiate_template( def, map )

    method instantiate_generic_template( ref_t:Token, def:Type )
      local specialized_lookup = Table<<String,String>>()  # for mapping placeholder names to the "as-if" specialized types (for generics)
      local generic_lookup = Table<<String,String>>()      # for mapping placeholder names to the actual generic types

      # Determine the type name of the generic base class in order to determine whether this will
      # be a generic mapping or a template instantiation.
      local parser = Parser( def.t.filepath, def.name.after_first("<<") )

      local generic_base_name_buffer = StringBuilder()
      generic_base_name_buffer.print( def.name.before_first('<') )
      generic_base_name_buffer.print( "<<" )

      local index = 0
      local first = true
      while (first or parser.consume(Token.symbol_comma))
        first = false

        if (index > placeholder_names.count)
          throw ref_t.error( "Too many substitution types specified; expected " + (placeholder_names.count) + "." )
        endIf

        if (index > 0) generic_base_name_buffer.print(',')

        if (generic_type_names[index]?)
          # This parameter is a type which may map to a generic base type.
          local specialized_type = parser.parse_type
          specialized_lookup[ placeholder_names[index] ] = specialized_type.name
          if (not specialized_type.is_functional and specialized_type.name != generic_type_names[index])
            local generic_base = Types.reference( def.t, generic_type_names[index] )
            generic_base_name_buffer.print( generic_base.name )
            generic_lookup[ placeholder_names[index] ] = generic_base.name
          else
            generic_base_name_buffer.print( specialized_type.name )
            generic_lookup[ placeholder_names[index] ] = specialized_type.name
          endIf
        else
          # This type parameter is replaced by a token list.
          local nesting_level = 1
          local parameter_buffer = StringBuilder()
          while (not (parser.next_is(Token.symbol_comma))) 
            if (not parser.reader.has_another) throw ref_t.error( "Missing closing '>>'." )

            if (parser.next_is( Token.symbol_open_specialize )) ++nesting_level

            if (parser.next_is( Token.symbol_close_specialize ))
              if (nesting_level == 1) escapeWhile
              else                    --nesting_level
            endIf

            local cur_t = parser.read
            #if (cur_t.type != Token.symbol_backslash) 
            generic_base_name_buffer.print( cur_t )
            parameter_buffer.print( cur_t )
          endWhile

          local parameter_type_name = parameter_buffer->String
          generic_lookup[ placeholder_names[index] ] = parameter_type_name
          specialized_lookup[ placeholder_names[index] ] = parameter_type_name
        endIf

        ++index
      endWhile
      parser.must_consume( Token.symbol_close_specialize )
      generic_base_name_buffer.print( ">>" )

      local generic_base_name = generic_base_name_buffer->String

      if (def.name == generic_base_name)
        # Create this type as a template instance
        instantiate_template( ref_t, def )
      else
        # Create this type as a shell that maps to the generic base
        create_generic_map
        create_generic_type( def, specialized_lookup, generic_lookup )
        def.generic_base = Types.reference( ref_t, generic_base_name )
      endIf

    method instantiate_list( def:Type, element_name:String, dims:Integer )
      create_generic_map

      local generic_base : Type
      if (dims == 1)
        local element_type_def = Types.reference( def.t, element_name )
        if (not element_type_def.is_functional)
          if (element_name != generic_type_names.first)
            generic_base = Types.reference( def.t, generic_type_names.first + "[]" )
          endIf
        endIf
      else
        generic_base = Types.reference( def.t, "Object[]" )
      endIf

      if (generic_base?)
        def.generic_base = generic_base
        local specialized_lookup = Table<<String,String>>()
        local generic_lookup = Table<<String,String>>()
        local specialized_name = element_name
        local remaining_dims = dims - 1
        while (remaining_dims > 0)
          specialized_name += "[]"
          --remaining_dims
        endWhile
        specialized_lookup[placeholder_names.first] = specialized_name
        generic_lookup[placeholder_names.first] = "Object"
        create_generic_type( def, specialized_lookup, generic_lookup )
      else
        local map = Table<<String,TokenList>>()
        local replacement_tokens = TokenList()
        replacement_tokens.add( IdentifierToken(t,element_name) )
        map[ placeholder_names.first ] = replacement_tokens
        instantiate_template( def, map )
      endIf

    method instantiate_template( def:Type, substitution_map:Table<<String,TokenList>> )
      local instance_tokens = Token[]( tokens.count )
      forEach (t1 in tokens)
        if (t1.type == Token.type_placeholder)
          local replacement_tokens = substitution_map[ t1->String ]
          if (replacement_tokens?)
            forEach (t2 in replacement_tokens) instance_tokens.add(t2.with_position_info(t1))
          else
            throw t1.error( "Use of undeclared placeholder name '" + t1 + "'." )
          endIf
        else
          instance_tokens.add( t1 )
        endIf
      endForEach

      add_augment_tokens( def, instance_tokens )

      def.attributes.add( attributes )

      #trace def.name, instance_tokens

      # Add base types
      Parser().parse_type_def( def, instance_tokens )
      #def.print

    method create_generic_type( def:Type, specialized_lookup:Table<<String,String>>, generic_lookup:Table<<String,String>> )
      local specialized_buffer = StringBuilder()
      local generic_target_buffer = StringBuilder()
      local id_buffer = StringBuilder()

      # Base types
      forEach (base_type in generic_map.base_types)
        def.base_types.add( base_type )
      endForEach

      # Attributes
      def.attributes = generic_map.attributes.clone

      # Properties
      forEach (generic_property in generic_map.property_list)
        specialized_buffer.clear
        generic_target_buffer.clear
        generic_target_buffer.clear
        local collecting_id = false
        forEach (ch in generic_property.type.name)
          if (collecting_id)
            contingent
              sufficient (ch >= 'a' and ch <= 'z')
              sufficient (ch >= 'A' and ch <= 'Z')
              sufficient (ch == '_')
              necessary (ch>='0' and ch<='9' and id_buffer.count?)
            satisfied
              # Still collecting ID
              id_buffer.print( ch )
            unsatisfied
              # ID is finished
              collecting_id = false

              local id = id_buffer->String
              id_buffer.clear
              specialized_buffer.print( specialized_lookup[id] )
              generic_target_buffer.print( generic_lookup[id] )

              specialized_buffer.print( ch )
              generic_target_buffer.print( ch )
            endContingent

          else
            if (ch == '$')
              id_buffer.print( '$' )
              collecting_id = true
            else
              specialized_buffer.print( ch )
              generic_target_buffer.print( ch )
            endIf
          endIf
        endForEach

        if (collecting_id)
          local id = id_buffer->String
          id_buffer.clear
          specialized_buffer.print( specialized_lookup[id] )
          generic_target_buffer.print( generic_lookup[id] )
        endIf
        
        local specialized_type = Types.reference( def.t, specialized_buffer->String )
        local specialized_property = Property( generic_property.t, def, generic_property.name, specialized_type )
        def.add_property( specialized_property )
#specialized_property.print
#.generic_base = generic_target_buffer->String
        #println( "$ -> $" (generic_property.type.name,specialized_type.name) )
      endForEach
#println( "generic properties in $: $" (def,def.property_list) )

      # Methods
      forEach (generic_m in generic_map.method_list)
        local generic_sig = generic_m.full_signature
        specialized_buffer.clear
        generic_target_buffer.clear
        specialized_buffer.print( "method " )
        local collecting_id = false
        forEach (ch in generic_sig)
          if (collecting_id)
            contingent
              sufficient (ch >= 'a' and ch <= 'z')
              sufficient (ch >= 'A' and ch <= 'Z')
              sufficient (ch == '_')
              necessary (ch>='0' and ch<='9' and id_buffer.count?)
            satisfied
              # Still collecting ID
              id_buffer.print( ch )
            unsatisfied
              # ID is finished
              collecting_id = false

              local id = id_buffer->String
              id_buffer.clear
              specialized_buffer.print( specialized_lookup[id] )
              generic_target_buffer.print( generic_lookup[id] )

              specialized_buffer.print( ch )
              generic_target_buffer.print( ch )
            endContingent

          else
            if (ch == '$')
              id_buffer.print( '$' )
              collecting_id = true
            else
              specialized_buffer.print( ch )
              generic_target_buffer.print( ch )
            endIf
          endIf
        endForEach

        if (collecting_id)
          local id = id_buffer->String
          id_buffer.clear
          specialized_buffer.print( specialized_lookup[id] )
          generic_target_buffer.print( generic_lookup[id] )
        endIf
        
        local m = Parser( def.t.filepath, specialized_buffer->String ).parse_method( def, true )
        m.attributes.add( generic_m.attributes )
        m.generic_base_signature = generic_target_buffer->String
        #println( "$.$ -> $.$" (def,m,m.type_context,m.generic_base_signature) )
      endForEach

      # Aliases
      if (generic_map.aliases?)
        def.aliases = Alias[]
        forEach (a in generic_map.aliases)
          def.aliases.add( a.clone_for_specializations(specialized_lookup) )
        endForEach
      endIf


endClass

