module ParseKit

class ParseReader<<$ID>>
  PROPERTIES
    tokens   : Token<<$ID>>[]
    position : Int32

  METHODS
    method init( tokens )

    method consume( token_type:TokenType<<$ID>> )->Logical
      if (not has_another or peek.type is not token_type) return false
      read
      return true

    method has_another->Logical
      return (position < tokens.count)

    method peek->Token<<$ID>>
      if (position == tokens.count) return Token<<$ID>>( TokenType<<$ID>>.EOI ).set_position( tokens.last )
      return tokens[ position ]

    method peek( lookahead:Int32 )->Token<<$ID>>
      local i = position + lookahead
      if (i >= tokens.count) return Token<<$ID>>( TokenType<<$ID>>.EOI ).set_position( tokens.last )
      return tokens[ i ]

    method read->Token<<$ID>>
      if (position == tokens.count) return Token<<$ID>>( TokenType<<$ID>>.EOI ).set_position( tokens.last )
      ++position
      return tokens[ position - 1 ]
endClass

