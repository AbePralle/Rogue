module ParseKit

$macro ParseRuleFn(LANGUAGE,BODY)
  function( rule:ParseRule<<LANGUAGE>> )->Cmd<<LANGUAGE>>
    BODY
  endFunction
$endMacro

class Parser<<$Language>>
  PROPERTIES
    reader    : TokenReader<<$Language>>
    rules     = StringTable<<ParseRule<<$Language>>>>()
    prev_rule : ParseRule<<$Language>>

  METHODS
    method add( rule:ParseRule<<$Language>> )->ParseRule<<$Language>>
      rules[ rule.name ] = rule
      prev_rule = rule
      rule.parser = this

      # Assign rule "xyz" to parse property "parse_xyz" if it exits
      local p = type_info.find_property( "parse_" + rule.name )
      if (p)
        introspector.set( p, Value(rule) )
      endIf

      return rule

    method chain( rule:ParseRule<<$Language>> )->ParseRule<<$Language>>
      if (prev_rule) prev_rule.next = rule
      return add( rule )

    method consume( token_type:TokenType<<$Language>> )->Logical [macro]
      this.reader.consume( token_type )

    method create_rule( name:String )->ParseRule<<$Language>>
      return ParseRule<<$Language>>(name)

    method create_rule( name:String, default_fn:Function(ParseRule<<$Language>>)->Cmd<<$Language>> )->ParseRule<<$Language>>
      local result = ParseRule<<$Language>>(name)
      result.on_default( default_fn )
      return result

    method create_binary_rule( name:String )->ParseRule<<$Language>>
      return BinaryParseRule<<$Language>>(name)

    method create_tokenizer->Tokenizer<<$Language>>
      return Tokenizer<<$Language>>()

    method has_another->Logical [macro]
      this.reader.has_another

    method must_consume( token_type:TokenType<<$Language>> ) [macro]
      this.reader.must_consume( token_type )

    method open( file:File )->this
      reader = TokenReader<<$Language>>( create_tokenizer.tokenize(file) )
      return this

    method open( filename:String, source:String )->this
      reader = TokenReader<<$Language>>( create_tokenizer.tokenize(filename,source) )
      return this

    method parse( file:File )
      open( file )
      parse( "program" )

    method parse( filename:String, source:String )
      open( filename, source )
      parse( "program" )

    method parse( rule_name:String )->Cmd<<$Language>>
      local rule = rules[ rule_name ]
      if (rule)
        return rule()
      else
        throw ParseError<<$Language>>( "[INTERNAL] No such parse rule: $." (rule_name) )
      endIf

    method peek->Token<<$Language>> [macro]
      this.reader.peek

    method peek( lookahead:Int32 )->Token<<$Language>> [macro]
      this.reader.peek( lookahead )

    method read->Token<<$Language>> [macro]
      this.reader.read


endClass

