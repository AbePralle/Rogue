class Console : PrintWriterAspect<<output_buffer>> [singleton]
  GLOBAL METHODS
    method input( prompt=null:String )->String
      if (prompt) Global.print( prompt ).flush

      native @|char st[4096];
              |fgets( st, 4096, stdin );
              |
              |// discard \n
              |int len = strlen( st );
              |if (len) st[--len] = 0;
              |
              |return RogueString_create_from_utf8( st, len );

  PROPERTIES
    output_buffer = StringBuilder()

  METHODS
    method write( characters:Character[] )->this
      native_print( characters )
      return this

    method write( value:String )->this
      native_print( value )
      return this

    method write( buffer:StringBuilder )->this
      native_print( buffer )
      return this

    $if ("C++")
    method native_print( utf8:Byte[] ) [macro]
      native 'RogueString_print_utf8( $utf8->data->as_bytes, $utf8->count )'

    method native_print( characters:Character[] ) [macro]
      native 'RogueString_print_characters( $characters->data->as_characters, $characters->count )'

    method native_print( value:String ) [macro]
      native 'RogueString_print_string( $value )'

    method native_print( value:StringBuilder ) [macro]
      native 'RogueString_print_utf8( $value->utf8->data->as_bytes, $value->utf8->count )'
    $endIf
endClass

