class Global : BufferedPrintWriter<<global_output_buffer>> [essential singleton]
  PROPERTIES
    console        = Console : PrintWriter
    exit_functions : (Function)[]

  METHODS
    method init
      native @|signal( SIGINT, RogueGlobal__on_control_c__Int32 );

      on_exit( function flush )

    method call_exit_functions [essential]
      # Called by the native layer when the program is ending normally or exiting
      # due to a System.exit(Int32) call.
      # Ensure these functions won't be called multiple times
      local functions = exit_functions
      exit_functions = null

      if (functions)
        forEach (fn in functions)
          fn()
        endForEach
      endIf

    method flush( buffer:StringBuilder )
      console.print( buffer ).flush
      buffer.clear

    method on_exit( fn:(Function) )
      ensure exit_functions
      exit_functions.add( fn )

  GLOBAL METHODS
    method on_control_c( signum:Int32 ) [essential]
      call_exit_functions
      System.exit 1
endClass

