class StringLookupList : LookupList<<String>>
  METHODS
    method add( key:String )
      this[ key ] = key

    method to->String[]
      return values
endClass

class LookupList<<$ValueType>>
  PROPERTIES
    indices = StringTable<<Int32>>()
    values  = $ValueType[]
    keys    = String[]

  METHODS
    method clear
      indices.clear
      values.clear
      keys.clear

    method contains( key:String )->Logical [macro]
      return this.indices.contains( key )

    method count->Int32
      return values.count

    method get( index:Int32 )->$ValueType
      return values[ index ]

    method get( key:String )->$ValueType
      local entry = indices.find( key )
      if (entry)
        return values[ entry.value ]
      else
        local default_value : $ValueType
        return default_value
      endIf

    method index( key:String )->Int32
      return indices[ key ]

    method remove( key:String )->$ValueType
      local default_value : $ValueType
      local entry = indices.find( key )
      if (entry)
        indices.remove( entry )
        local index = entry.value
        local result = values[ index ]
        values.remove_at( index )
        keys.remove_at( index )
        return result
      else
        return default_value
      endIf

    method remove_at( index:Int32 )->$ValueType
      return remove( keys[index] )

    method set( index:Int32, value:$ValueType ) [macro]
      this.values[ index ] = value

    method set( key:String, value:$ValueType )
      local entry = indices.find( key )
      if (entry)
        values[ entry.value ] = value
      else
        indices[ key ] = values.count
        values.add( value )
        keys.add( key )
      endIf
endClass
