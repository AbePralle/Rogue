class Real [primitive]
  ROUTINES
    routine create( bits:Long )->Real
      $if ("C++")
        native "return *((RogueReal*)&$bits);"
      $endIf

  METHODS
    method hash_code->Integer
      return Long(this)->Integer

    method to->String
      return "" + this

endClass

class Float [primitive]
endClass

class Long [primitive]
  ROUTINES
    routine create( bit_source:Real )->Long
      $if ("C++")
        native "return *((RogueLong*)&$bit_source);"
      $endIf

  METHODS
    method hash_code->Integer
      return this

    method to->String
      return StringBuilder().print( this )->String
endClass

class Integer [primitive]
  METHODS
    method hash_code->Integer
      return this

    method to->Long
      $if ("C++") macro native "((RogueLong)$this)"

    method to->String
      return "" + this
endClass

class Character [primitive]
  METHODS
    method hash_code->Integer
      return this->Integer

    method is_alphanumeric->Logical
      return (this >= '0' and this <= '9') or
             (this >= 'a' and this <= 'z') or
             (this >= 'A' and this <= 'Z')

    method is_identifier->Logical
      return (this >= '0' and this <= '9') or
             (this >= 'a' and this <= 'z') or
             (this >= 'A' and this <= 'Z') or
             (this == '_')

    method is_letter->Logical
      return (this >= 'a' and this <= 'z') or
             (this >= 'A' and this <= 'Z')

    method is_number( base=10:Integer )->Logical
      return to_number(base) != -1

    method to->String
      return "" + this

    method to_number( base=10:Integer )->Integer
      if (this >= '0' and this < '0'+base)      return (this - '0')
      if (this >= 'a' and this < ('a'-10)+base) return (this - 'a') + 10
      if (this >= 'A' and this < ('A'-10)+base) return (this - 'A') + 10
      return -1
endClass

class Byte [primitive]
  METHODS
    method hash_code->Integer
      return this->Integer

    method to->String
      return "" + this
endClass

class Logical [primitive]
  METHODS
    method hash_code->Integer
      if (this) return 1
      else      return 0

    method to->String
      return "" + this
endClass

