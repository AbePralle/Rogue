class PropertyValue
  GLOBAL METHODS
    method create( value:Int32 )->PropertyValue
      return Int32Property( value )

    method create( value:Logical )->PropertyValue
      return select{ value:LogicalProperty.true_value || LogicalProperty.false_value }

    method create( value:Real64 )->PropertyValue
      return Real64Property( value )

    method create( value:Object )->PropertyValue
      return ObjectProperty( value )

    method create( value:String )->PropertyValue
      return StringProperty( value )

  METHODS
    method add( value:Int32 )->PropertyValue [macro]
      this.add( PropertyValue(value) )

    method add( value:Logical )->PropertyValue [macro]
      this.add( PropertyValue(value) )

    method add( value:Real64 )->PropertyValue [macro]
      this.add( PropertyValue(value) )

    method add( value:Object )->PropertyValue [macro]
      this.add( PropertyValue(value) )

    method add( value:String )->PropertyValue [macro]
      this.add( PropertyValue(value) )

    method add( value:PropertyValue )->PropertyValue
      return this

    method add( other:PropertyValue[] )->PropertyValue
      return this

    method clear->PropertyValue
      return this

    method clone->PropertyValue
      return this

    method contains( key:String )->Logical
      return false

    method count->Int32
      return 0

    method ensure_list( key:String )->PropertyList
      # When called on a PropertyTable, ensures that the table contains a list
      # with the given key, creating it if necessary, and returns that list.
      # When called on any other type of PropertyValue, a new PropertyList
      # will be returned but it won't be stored in a table.
      return @[]

    method ensure_table( key:String )->PropertyTable
      # When called on a PropertyTable, ensures that the table contains a table
      # with the given key, creating it if necessary, and returns that table.
      # When called on any other type of PropertyValue, a new PropertyTable
      # will be returned but it won't be stored in a table.
      return @{}

    method first->PropertyValue
      return null

    method get( index:Int32 )->PropertyValue
      return null

    method get( key:String )->PropertyValue
      return null

    method get_int32( index:Int32 )->Int32
      local value = get( index )
      if (not value) return 0
      return value->Int32

    method get_logical( index:Int32 )->Logical
      local value = get( index )
      if (not value) return false
      return value->Logical

    method get_real64( index:Int32 )->Real64
      local value = get( index )
      if (not value) return 0.0
      return value->Real64

    method get_object( index:Int32 )->Object
      local value = get( index )
      if (not value) return null
      return value->Object

    method get_string( index:Int32 )->String
      local value = get( index )
      if (not value) return ""
      return value->String

    method get_list( index:Int32 )->PropertyList
      local value = get( index ) as PropertyList
      if (value) return value
      else       return PropertyList()

    method get_table( index:Int32 )->PropertyTable
      local value = get( index ) as PropertyTable
      if (value) return value
      else       return PropertyTable()

    method get_int32( key:String )->Int32
      local value = get( key )
      if (not value) return 0
      return value->Int32

    method get_list( key:String )->PropertyList
      local value = get( key ) as PropertyList
      if (value) return value
      else       return PropertyList()

    method get_logical( key:String )->Logical
      local value = get( key )
      if (not value) return false
      return value->Logical

    method get_real64( key:String )->Real64
      local value = get( key )
      if (not value) return 0.0
      return value->Real64

    method get_object( key:String )->Object
      local value = get( key )
      if (not value) return null
      return value->Object

    method get_string( key:String )->String
      local value = get( key )
      if (not value) return ""
      return value->String

    method get_table( key:String )->PropertyTable
      local value = get( key ) as PropertyTable
      if (value) return value
      else       return PropertyTable()

    method is_collection->Logical
      return false

    method is_complex->Logical
      if (not is_collection) return false
      if (count > 1)         return true
      forEach (value in this)
        if (value and value.is_complex) return true
      endForEach
      return false

    method is_int32->Logical
      return false

    method is_list->Logical
      return false

    method is_logical->Logical
      return false

    method is_number->Logical
      return false

    method is_object->Logical
      return false

    method is_real64->Logical
      return false

    method is_string->Logical
      return false

    method is_table->Logical
      return false

    method last->PropertyValue
      return null

    method operator-()->PropertyValue
      return PropertyValue( 0 )

    method operator+( other:PropertyValue )->PropertyValue
      return PropertyValue( 0 )

    method operator-( other:PropertyValue )->PropertyValue
      return PropertyValue( 0 )

    method operator*( other:PropertyValue )->PropertyValue
      return PropertyValue( 0 )

    method operator/( other:PropertyValue )->PropertyValue
      return PropertyValue( 0 )

    method operator%( other:PropertyValue )->PropertyValue
      return PropertyValue( 0 )

    method operator+( other:Real64 )->PropertyValue
      return PropertyValue( 0 )

    method operator-( other:Real64 )->PropertyValue
      return PropertyValue( 0 )

    method operator*( other:Real64 )->PropertyValue
      return PropertyValue( 0 )

    method operator/( other:Real64 )->PropertyValue
      return PropertyValue( 0 )

    method operator%( other:Real64 )->PropertyValue
      return PropertyValue( 0 )

    method operator+( other:String )->PropertyValue
      return PropertyValue( 0 )

    method operator*( other:String )->PropertyValue
      return PropertyValue( 0 )

    method remove( value:PropertyValue )->PropertyValue
      return value

    method remove_at( index:Int32 )->PropertyValue
      return null

    method remove_first->PropertyValue
      return null

    method remove_last->PropertyValue
      return null

    method save( file:File, formatted=false:Logical )->Logical
      return file.save( to_json(formatted) )

    method set( index:Int32, value:Int32 )->this [macro]
      this.set( index, PropertyValue(value) )

    method set( index:Int32, value:Logical )->this [macro]
      this.set( index, PropertyValue(value) )

    method set( index:Int32, value:Real64 )->this [macro]
      this.set( index, PropertyValue(value) )

    method set( index:Int32, value:Object )->this [macro]
      this.set( index, PropertyValue(value) )

    method set( index:Int32, value:String )->this [macro]
      this.set( index, PropertyValue(value) )

    method set( index:Int32, value:PropertyValue )->this
      return this

    method set( key:String, value:Int32 )->this [macro]
      this.set( key, PropertyValue(value) )

    method set( key:String, value:Logical )->this [macro]
      this.set( key, PropertyValue(value) )

    method set( key:String, value:Real64 )->this [macro]
      this.set( key, PropertyValue(value) )

    method set( key:String, value:Object )->this [macro]
      this.set( key, PropertyValue(value) )

    method set( key:String, value:String )->this [macro]
      this.set( key, PropertyValue(value) )

    method set( key:String, value:PropertyValue )->this
      return this

    method to->Int32
      return 0

    method to->Logical
      return (this->Int32)

    method to->Real64
      return this->Int32->Real64

    method to->Object
      return null

    method to_json( formatted=false:Logical )->String
      return to_json( StringBuilder(), formatted )->String

    method to_json( buffer:StringBuilder, formatted=false:Logical )->StringBuilder
      return buffer
endClass

class Int32Property( value:Int32 ) : PropertyValue
  METHODS
    method is_int32->Logical
      return true

    method is_number->Logical
      return true

    method operator-()->PropertyValue
      return Int32Property( -value )

    method operator+( other:PropertyValue )->PropertyValue
      if (not other) return PropertyValue(0)
      return Int32Property( value + other->Int32 )

    method operator-( other:PropertyValue )->PropertyValue
      if (not other) return PropertyValue(0)
      return Int32Property( value - other->Int32 )

    method operator*( other:PropertyValue )->PropertyValue
      if (not other) return PropertyValue(0)
      return Int32Property( value * other->Int32 )

    method operator/( other:PropertyValue )->PropertyValue
      if (not other) return PropertyValue(0)
      return Int32Property( value / other->Int32 )

    method operator%( other:PropertyValue )->PropertyValue
      if (not other) return PropertyValue(0)
      return Int32Property( value % other->Int32 )

    method operator+( other:Real64 )->PropertyValue
      if (not other) return PropertyValue(0)
      return Int32Property( value + other )

    method operator-( other:Real64 )->PropertyValue
      if (not other) return PropertyValue(0)
      return Int32Property( value + other )

    method operator*( other:Real64 )->PropertyValue
      if (not other) return PropertyValue(0)
      return Int32Property( value + other )

    method operator/( other:Real64 )->PropertyValue
      if (not other) return PropertyValue(0)
      return Int32Property( value + other )

    method operator%( other:Real64 )->PropertyValue
      if (not other) return PropertyValue(0)
      return Int32Property( value + other )

    method operator+( other:String )->PropertyValue
      if (not other) return PropertyValue(0)
      return StringProperty( value + other )

    method operator*( other:String )->PropertyValue
      if (not other) return PropertyValue(0)
      return StringProperty( value * other )

    method to->Int32
      return value

    method to->String
      return value->String

    method to_json( buffer:StringBuilder, formatted=false:Logical )->StringBuilder
      buffer.print( value )
      return buffer
endClass

class LogicalProperty( value:Logical ) : PropertyValue
  GLOBAL PROPERTIES
    true_value  = LogicalProperty( true )
    false_value = LogicalProperty( false )

  METHODS
    method is_logical->Logical
      return true

    method to->Int32
      if (value) return 1
      else       return 0

    method to->Logical
      return value

    method to->String
      return value->String

    method to_json( buffer:StringBuilder, formatted=false:Logical )->StringBuilder
      buffer.print( value )
      return buffer
endClass

class Real64Property( value:Real64 ) : PropertyValue
  METHODS
    method is_number->Logical
      return true

    method is_real64->Logical
      return true

    method operator-()->PropertyValue
      return Real64Property( -value )

    method operator+( other:PropertyValue )->PropertyValue
      if (not other) return PropertyValue(0)
      return Real64Property( value + other->Real64 )

    method operator-( other:PropertyValue )->PropertyValue
      if (not other) return PropertyValue(0)
      return Real64Property( value - other->Real64 )

    method operator*( other:PropertyValue )->PropertyValue
      if (not other) return PropertyValue(0)
      return Real64Property( value * other->Real64 )

    method operator/( other:PropertyValue )->PropertyValue
      if (not other) return PropertyValue(0)
      return Real64Property( value / other->Real64 )

    method operator%( other:PropertyValue )->PropertyValue
      if (not other) return PropertyValue(0)
      return Real64Property( value % other->Real64 )

    method operator+( other:Real64 )->PropertyValue
      if (not other) return PropertyValue(0)
      return Real64Property( value + other )

    method operator-( other:Real64 )->PropertyValue
      if (not other) return PropertyValue(0)
      return Real64Property( value + other )

    method operator*( other:Real64 )->PropertyValue
      if (not other) return PropertyValue(0)
      return Real64Property( value + other )

    method operator/( other:Real64 )->PropertyValue
      if (not other) return PropertyValue(0)
      return Real64Property( value + other )

    method operator%( other:Real64 )->PropertyValue
      if (not other) return PropertyValue(0)
      return Real64Property( value + other )

    method operator+( other:String )->PropertyValue
      if (not other) return PropertyValue(0)
      return StringProperty( value + other )

    method operator*( other:String )->PropertyValue
      if (not other) return PropertyValue(0)
      return StringProperty( Int32(value) * other )

    method to->Int32
      return value

    method to->Real64
      return value

    method to->String
      return value->String

    method to_json( buffer:StringBuilder, formatted=false:Logical )->StringBuilder
      buffer.print( value )
      return buffer
endClass

class ObjectProperty( value:Object ) : PropertyValue
  METHODS
    method is_object->Logical
      return true

    method to->Object
      return value

    method to->String
      return value->String

    method to_json( buffer:StringBuilder, formatted=false:Logical )->StringBuilder
      buffer.print( value->String )
      return buffer
endClass

class StringProperty( value:String ) : PropertyValue
  GLOBAL PROPERTIES
    empty_string = StringProperty( "" )

  METHODS
    method is_string->Logical
      return true

    method operator+( other:PropertyValue )->PropertyValue
      if (not other) return PropertyValue(0)
      return StringProperty( value + other->String )

    method operator*( other:PropertyValue )->PropertyValue
      if (not other) return PropertyValue(0)
      return StringProperty( value * other->Int32 )

    method operator+( other:Real64 )->PropertyValue
      if (not other) return PropertyValue(0)
      return StringProperty( value + other )

    method operator+( other:String )->PropertyValue
      if (not other) return PropertyValue(0)
      return StringProperty( value + other )

    method to->Character
      if (value.count > 0) return value[0]
      else                 return 0->Character

    method to->Int32
      return value->Int32

    method to->Logical
      return (value == "true" or value == "TRUE" or value == "yes" or value == "YES" or value == "1")

    method to->Real64
      return value->Real64

    method to->String
      return value

    method to_json( buffer:StringBuilder, formatted=false:Logical )->StringBuilder
      return to_json( value, buffer, formatted )

  GLOBAL METHODS
    method to_json( value:String, buffer:StringBuilder, formatted=false:Logical )->StringBuilder
      if (value)
        buffer.print '"'
        forEach (ch in value)
          which (ch)
            case '"':
              buffer.print( "\\\"" )
            case '\\':
              buffer.print( "\\\\" )
            case '\b':
              buffer.print( "\\b" )
            case '\f':
              buffer.print( "\\f" )
            case '\n':
              buffer.print( "\\n" )
            case '\r':
              buffer.print( "\\r" )
            case '\t':
              buffer.print( "\\t" )
            others
              if (ch >= 32 and ch <= 126)
                buffer.print( ch )
              elseIf (ch < 32 or ch == 127 or ch == 0x2028 or ch == 0x2029)
                # RE: 2028/9:
                # http://stackoverflow.com/questions/2965293/javascript-parse-error-on-u2028-unicode-character
                buffer.print( "\\u" )
                local n = ch : Int32
                forEach (nibble in 0..3)
                  local digit = (n :>>>: 12) & 15
                  n = n:<<:4
                  if (digit <= 9)
                    buffer.print( digit )
                  else
                    buffer.print( ('a' + (digit - 10))->Character )
                  endIf
                endForEach
              else
                # Store printable Unicode without encoding as \\uXXXX
                buffer.print( ch )
              endIf
          endWhich
        endForEach
        buffer.print '"'
      else
        buffer.print "null"
      endIf
      return buffer
endClass

class PropertyList : PropertyValue
  PROPERTIES
    values : PropertyValue[]

  METHODS
    method init
      init( 10 )

    method init( initial_capacity:Int32 )
      values = PropertyValue[]( initial_capacity )

    method init( values )

    method clone->PropertyList
      return PropertyList( values.clone )

    method add( value:PropertyValue )->PropertyList
      values.add( value )
      return this

    method add( other:PropertyValue[] )->PropertyList
      values.reserve(other.count)
      forEach (value in other) values.add( value )
      return this

    method clear->PropertyList
      values.clear
      return this

    method count->Int32
      return values.count

    method first->PropertyValue
      return this.get(0)

    method get( index:Int32 )->PropertyValue
      if (index < 0 or index >= values.count) return null
      return values[ index ]

    method is_collection->Logical
      return true

    method is_list->Logical
      return true

    method last->PropertyValue
      return this.get( values.count - 1 )

    method remove( value:PropertyValue )->PropertyValue
      return values.remove( value )

    method remove_at( index:Int32 )->PropertyValue
      return values.remove_at( index )

    method remove_first->PropertyValue
      return values.remove_first

    method remove_last->PropertyValue
      return values.remove_last

    method set( index:Int32, new_value:PropertyValue )->PropertyValue
      if (index < 0) return this

      while (index >= values.count) add( null as PropertyValue )
      values.set( index, new_value )

      return this

    method to->String
      return values->String

    method to_json( buffer:StringBuilder, formatted=false:Logical )->StringBuilder
      local pretty_print = (formatted and is_complex)

      buffer.print( '[' )

      if (pretty_print)
        buffer.println
        buffer.indent += 2
      endIf

      local first = true
      forEach (value in values)
        if (first)
          first = false
        else
          buffer.print( ',' )
          if (pretty_print) buffer.println
        endIf

        if (value) value.to_json( buffer, formatted )
        else       buffer.print( "null" )
      endForEach

      if (pretty_print)
        buffer.println
        buffer.indent -= 2
      endIf

      buffer.print( ']' )
      return buffer

    GLOBAL METHODS
      method load( file:File )->PropertyList
        if (not file.exists) return PropertyList()
        return JSON.parse_list( file.load_as_string )

      method parse( json:String )->PropertyList
        return JSON.parse_list( json )
endClass

class PropertyTable : PropertyValue
  PROPERTIES
    values    : Table<<String,PropertyValue>>

  METHODS
    method init
      values = Table<<String,PropertyValue>>()

    method init( values )

    method clear->this
      values.clear
      return this

    method clone->PropertyTable
      return PropertyTable( values.clone )

    method contains( key:String )->Logical
      return values.contains( key )

    method count->Int32
      return values.count

    method ensure_list( key:String )->PropertyList
      # Ensures that this table contains a list with the given key, creating
      # it if necessary, and returns that list.
      local list = this[ key ] as PropertyList
      if (list) return list
      list = @[]
      this[ key ] = list
      return list

    method ensure_table( key:String )->PropertyTable
      # Ensures that this table contains a table with the given key, creating
      # it if necessary, and returns that table.
      local table = this[ key ] as PropertyTable
      if (table) return table
      table = @{}
      this[ key ] = table
      return table

    method get( index:Int32 )->PropertyValue
      if (index < 0 or index >= count) return null
      return values[ keys[index] ]

    method get( key:String )->PropertyValue
      return values[ key ]

    method is_collection->Logical
      return true

    method is_table->Logical
      return true

    method keys->String[] [macro]
      values.keys

    method remove( key:String )->PropertyValue
      return values.remove( key )

    method remove_at( index:Int32 )->PropertyValue
      if (index < 0 or index >= count) return null
      return values.remove( values.keys[index] )

    method set( key:String, new_value:PropertyValue )->this
      values.set( key, new_value )
      return this

    method to->String
      return values->String

    method to_json( buffer:StringBuilder, formatted=false:Logical )->StringBuilder
      local pretty_print = (formatted and is_complex)

      buffer.print( '{' )

      if (pretty_print)
        buffer.println
        buffer.indent += 2
      endIf

      local first = true
      forEach (key in values.keys)
        if (first)
          first = false
        else
          buffer.print( ',' )
          if (pretty_print) buffer.println
        endIf

        StringProperty.to_json( key, buffer, formatted )
        buffer.print( ':' )

        local value = values[key]
        if (pretty_print and value and value.is_complex) buffer.println

        if (value) value.to_json( buffer, formatted )
        else       buffer.print( "null" )
      endForEach

      if (pretty_print) buffer.println; buffer.indent -= 2

      buffer.print( '}' )
      return buffer

    GLOBAL METHODS
      method load( file:File )->PropertyTable
        if (not file.exists) return PropertyTable()
        return JSON.parse_table( file.load_as_string )

      method parse( json:String )->PropertyTable
        return JSON.parse_table( json )
endClass

