class System [requisite]
  GLOBAL PROPERTIES
    command_line_arguments = String[]
    executable_filepath    : String
    environment            : SystemEnvironment

  GLOBAL METHODS
    method exit( result_code:Int32 )
      $if ("C++")
        native "exit( $result_code );"
      $endIf

    method run( command:String ) [macro]
      native
        @|{
         |  char cmd_buffer[4096];
         |  RogueString_to_c_string( $command, cmd_buffer, 4096 );
         |  system( cmd_buffer );
         |}

    method time->Real64
      $if ("C++")
        native @|#if defined(_WIN32)
                |  struct __timeb64 time_struct;
                |  RogueReal64 time_seconds;
                |  _ftime64_s( &time_struct );
                |  time_seconds = (RogueReal64) time_struct.time;
                |  time_seconds += time_struct.millitm / 1000.0;
                |  return time_seconds;
                |
                |#else
                |  struct timeval time_struct;
                |  RogueReal64 time_seconds;
                |  gettimeofday( &time_struct, 0 );
                |  time_seconds = (RogueReal64) time_struct.tv_sec;
                |  time_seconds += (time_struct.tv_usec / 1000000.0);
                |  return time_seconds;
                |#endif
      $endIf
endClass

class SystemEnvironment [compound]
  METHODS
    method get( name:String )->String
      local result : String

      native @|char buffer[128];
              |RogueString_to_c_string( $name, buffer, 128 );
              |char* c_result = getenv( buffer );
              |if (c_result)
              |{
              |  $result = RogueString_create_from_c_string( c_result );
              |}

      return select{ not result:null, result.decode_utf8 }

    method listing->String[]
      nativeHeader extern char **environ;

      local result = String[]
      native "char** env = environ;"
      while (native("*env")->Logical)
        result.add( native("RogueString_create_from_c_string( *(env++) )")->String.before_first('=') )
      endWhile

      return result;

    method set( name:String, value:String )
      native @|char name_buffer[128];
              |RogueString_to_c_string( $name, name_buffer, 128 );

      if (value and value.count)
        value = value.to_utf8
        if (value.count < 4096)
          native @|char value_buffer[4096];
                  |RogueString_to_c_string( $value, value_buffer, 4096 );
                  |setenv( name_buffer, value_buffer, 1 );
        else
          native @|char* value_buffer = new char[ $value->count ];
                  |RogueString_to_c_string( $value, value_buffer, $value->count+1 );
                  |setenv( name_buffer, value_buffer, 1 );
                  |delete value_buffer;
        endIf
      else
        native @|unsetenv( name_buffer );
      endIf
endClass
