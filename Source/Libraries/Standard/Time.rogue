class Stopwatch
  PROPERTIES
    start_time = System.time: Real64

  METHODS
    method add( delta_time:Real64 )->this
      start_time -= delta_time
      return this

    method elapsed->Real64
      return (System.time - start_time)

    method restart->this
      start_time = System.time
      return this

    method subtract( delta_time:Real64 )->this
      return add( -delta_time )

    method to->String
      return StringBuilder().print( elapsed, 2 ).print( " seconds" )->String
endClass


class Timer
  PROPERTIES
    duration                 : Real64
    start_time = System.time : Real64

  METHODS
    method init( duration )

    method add( delta_time:Real64 )->this
      start_time += delta_time
      return this

    method elapsed->Real64
      return (System.time - start_time)

    method is_expired->Logical
      return elapsed >= duration

    method remaining->Real64
      return duration - elapsed

    method restart->this
      start_time = System.time
      return this

    method subtract( delta_time:Real64 )->this
      return add( -delta_time )

    method to->String
      return StringBuilder().print( elapsed, 2 ).print( '/' ).print( duration, 2 ).print( " seconds" )->String
endClass

