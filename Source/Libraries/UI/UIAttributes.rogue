uses UI

class UIAttributes [singleton]
  METHODS
    method alignment( component:UIComponent )->Anchor
      if (component.attributes)
        local alignment = component.attributes.alignment
        if (alignment) return alignment.value
      endIf
      if (component.parent) return component.parent.alignment
      return Anchor.TOP_LEFT

    method anchor( component:UIComponent )->Anchor
      if (component.attributes)
        local anchor = component.attributes.anchor
        if (anchor) return anchor.value
      endIf
      return Anchor.TOP_LEFT

    method font( component:UIComponent )->UIFont
      if (component.attributes)
        local font = component.attributes.font
        if (font) return font
      endIf
      if (component.parent) return component.parent.font
      return UI.default_font

    method weight( component:UIComponent )->XY
      if (component.attributes) return component.attributes.weight
      else                      return XY(1,1)
endClass

class UIAttribute
  GLOBAL METHODS
    method operator+( left:UIAttribute, right:UIAttribute )->UIAttribute
      if (not left)  return right
      if (not right) return left

      # Remove any attribute of the same type if it exists
      local new_type_name = right.type_name
      if (left.type_name is new_type_name)
        return left.next + right
      else
        local prev = null : UIAttribute
        local cur = left
        while (cur)
          if (cur.type_name is new_type_name)
            # Remove old attribute of same type
            prev.next = cur.next
            return prev + right
          endIf
          prev = cur
          cur .= next
        endWhile
      endIf

      local cur = left
      while (cur.next) cur .= next

      cur.next = right
      return left

  PROPERTIES
    next : UIAttribute

  METHODS
    method alignment->Anchor?
      if (next) return next.alignment
      return null

    method anchor->Anchor?
      if (next) return next.anchor
      return null

    method font->UIFont
      if (next) return next.font
      return null

    method weight->XY
      if (next) return next.weight
      return XY(1,1)
endClass

class UIAlignmentAttribute( value:Anchor ) : UIAttribute
  METHODS
    method alignment->Anchor?
      return value
endClass

class UIAnchorAttribute( value:Anchor ) : UIAttribute
  METHODS
    method anchor->Anchor?
      return value
endClass

class UIFontAttribute( value:UIFont ) : UIAttribute
  METHODS
    method font->UIFont
      return value
endClass

class UIWeightAttribute( value:XY ) : UIAttribute
  METHODS
    method weight->XY
      return value
endClass
