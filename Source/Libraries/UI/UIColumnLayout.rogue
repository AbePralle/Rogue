module UI

class UIColumnLayout : UILayout
  METHODS
    method horizontal_flex->Real64
      return 0.0

    method maximum_width( &subcomponents_only )->Real64
      if (not subcomponents_only)
        local fixed_x = content_size.x
        if (fixed_x) return fixed_x
      endIf
      if (not first_child) return 0

      local max_width = 0
      forEachChild (component,this)
      local w = component.maximum_width
      if (w == 0) return 0
      max_width = max_width.or_larger( w )
      endForEachChild (component)
      return max_width

    method maximum_height( &subcomponents_only )->Real64
      if (not subcomponents_only)
        local fixed_y = content_size.y
        if (fixed_y) return fixed_y
      endIf
      if (not first_child) return 0

      local max_height = 0
      forEachChild (component,this)
      local h = component.maximum_height
      if (h == 0) return 0
      max_height += h
      endForEachChild (component)
      return max_height

    method minimum_width( &subcomponents_only )->Real64
      if (not subcomponents_only)
        local fixed_x = content_size.x
        if (fixed_x) return fixed_x
      endIf
      if (not first_child) return 0

      local min_width = 0
      forEachChild (component,this)
      min_width = min_width.or_larger( component.minimum_width )
      endForEachChild (component)
      return min_width

    method minimum_height( &subcomponents_only )->Real64
      if (not subcomponents_only)
        local fixed_y = content_size.y
        if (fixed_y) return fixed_y
      endIf
      if (not first_child) return 0

      local min_height = 0
      forEachChild (component,this)
      min_height += component.minimum_height
      endForEachChild (component)
      return min_height

    method operator+( right_component:UIComponent )->UIComponent
      return UILayout.h.[ add(this), add(right_component) ]

    method operator/( bottom_component:UIComponent )->UIComponent
      add( bottom_component )
      return this

    method update_subcomponent_layout
      local content_height = this.minimum_height( &subcomponents_only )
      local max_height = this.maximum_height

      local min_height = this.size.y
      if (min_height < content_height) min_height = content_height
      if (max_height and min_height > max_height) min_height = max_height

      if (min_height > content_height)
        # Distribute extra height (beyond minimum) to components before laying out
        local extra = min_height - content_height
        local total_flex = this.vertical_flex
        forEachChild (component,this)
        local flex = component.vertical_flex
        if (flex)
          local h = Int32( extra * flex / total_flex )
          total_flex -= flex
          component.@size = XY(0,component.minimum_height+h)
          extra -= h
        else
          component.@size = component.minimum_size
        endIf
        endForEachChild (component)

      else
        # All components get set to their minimum height
        forEachChild (component,this)
        component.@size = component.minimum_size
        endForEachChild (component)
      endIf

      local y = @position.y
      forEachChild (component,this)
      local min_width = component.minimum_width
      local max_width = component.maximum_width
      local content_width = this.size.x
      if (content_width < min_width) content_width = min_width
      if (max_width and content_width > max_width) content_width = max_width
      local x = @position.x
      local h = component.size.y
      component.update_layout( Box(x,y,this.size.x,h).subset(XY(content_width,h),component.anchor)+offset )
      y += h
      endForEachChild (component)

    method vertical_flex->Real64
      local flex = 0.0
      if (first_child)
        forEachChild (component,this)
          flex += component.vertical_flex
        endForEachChild (component)
      endIf
      return flex
endClass

