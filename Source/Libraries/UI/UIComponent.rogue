module UI
  uses Utility/Node

class UIComponent : Node<<UIComponent>>
  PROPERTIES
    position     : XY
    size         : XY
    fixed_size   : XY
    anchor       = Anchor.CENTER : Anchor

    is_modified  = true
    needs_draw   : Logical
    needs_render : Logical
    renderer     : UIRenderer?

  METHODS
    method init

    method init( fixed_size, content=null:UIComponent )
      init( XY.zero, fixed_size, content )

    method init( position, fixed_size, content=null:UIComponent )
      if (content) add( content )

    method init( bounds:Box, content=null:UIComponent )
      init( bounds.position, bounds.size, content )

    method init( content:UIComponent )
      add( content )

    method after_buffer
      # Called at the end of a buffer operation after any children have been drawn
      noAction

    method after_render
      # Called at the end of a render operation after any children have been drawn
      noAction

    method assign_renderer [propagated]
      UITheme._dispatch_assign_renderer( this )

    method bounds->Box
      return Box( position, size )

    method compute_bounds( parent_bounds:Box )->Box
      if (fixed_size.x)
        if (fixed_size.y)
          return Box( position, fixed_size )
        else
          return Box( position.x, parent_bounds.position.y, fixed_size.x, parent_bounds.size.y )
        endIf
      else
        if (fixed_size.x)
          return Box( parent_bounds.position.x, position.y, parent_bounds.size.x, fixed_size.y )
        else
          return parent_bounds
        endIf
      endIf

    method draw
      update_layout

      if (not renderer.exists) assign_renderer

      if (needs_draw)
        needs_render = true
        on_buffer
      elseIf (UIManager.render_every_frame)
        needs_render = true
      endIf

      if (needs_render)
        on_render
      endIf

      forEachChild (cur,this,cur.draw)

      if (needs_draw)
        needs_draw = false
        after_buffer
      endIf

      if (needs_render)
        needs_render = false
        after_render
      endIf

    method horizontal_flex->Real64
      if (fixed_size.x) return 0.0
      else              return 1.0

    method invalidate
      mark_modified

    method maximum_height( &subcomponents_only )->Real64
      return fixed_size.y

    method maximum_size( &subcomponents_only )->XY
      return XY( maximum_width, maximum_height )

    method maximum_width( &subcomponents_only )->Real64
      return fixed_size.x

    method minimum_height( &subcomponents_only )->Real64
      return fixed_size.y

    method minimum_size( &subcomponents_only )->XY
      return XY( minimum_width, minimum_height )

    method minimum_width( &subcomponents_only )->Real64
      return fixed_size.x

    method on_buffer
      # Called at the beginning of a buffer operation before any children have been drawn
      _dispatch_buffer

    method on_render
      # Called at the beginning of a render operation before any children have been drawn
      _dispatch_render

    method on_update
      noAction

    method operator+( right_component:UIComponent )->UIComponent
      return UILayout.h.[ add(this), add(right_component) ]

    method operator/( bottom_component:UIComponent )->UIComponent
      return UILayout.v.[ add(this), add(bottom_component) ]

    method redraw
      needs_draw = true
      redraw_area( bounds )

    method redraw_area( area:Box )
      # Propagate area to root
      if (parent) parent.redraw_area( area )

    method set_fixed_size( new_fixed_size:XY )
      if (@fixed_size != new_fixed_size) is_modified = true
      @fixed_size = new_fixed_size
      @size = new_fixed_size

    method set_size( new_fixed_size:XY )
      set_fixed_size( new_fixed_size )

    method update
      on_update
      update_layout
      forEachChild (cur,this,cur.update)

    method update_subcomponent_layout
      local bounds = this.bounds
      forEachChild (cur,this,cur.update_layout(bounds))

    method update_draw_buffer
      needs_draw = false
      needs_render = true

    method update_layout
      if (is_modified) update_layout( bounds )

    method update_layout( new_bounds:Box )
      if (is_modified or new_bounds != bounds)
        is_modified = false
        needs_draw = true
        @size = XY( which{fixed_size.x||new_bounds.width}, which{fixed_size.y||new_bounds.height} ).floor
        position = bounds.positioned_within( new_bounds ).position.floor
        update_subcomponent_layout
      endIf

    method vertical_flex->Real64
      if (fixed_size.y) return 0.0
      else              return 1.0

    method _assign_renderer<<$Theme>>( theme:$Theme ) [propagated]
      renderer = (UITheme as $Theme).renderer( this )

    method _dispatch_buffer [propagated]
      if (renderer.exists and renderer.value)
        # The renderer can exist and be null
        renderer.value.buffer( this )
      endIf

    method _dispatch_render [propagated]
      if (renderer.exists and renderer.value)
        # The renderer can exist and be null
        renderer.value.render( this )
      endIf
endClass

