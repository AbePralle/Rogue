module UI

class UIRowLayout : UILayout
  METHODS
    method horizontal_flex->Real64
      local flex = 0.0
      if (first_child)
        forEachChild (component,this)
          flex += component.horizontal_flex
        endForEachChild (component)
      endIf
      return flex

    method maximum_width( &subcomponents_only )->Real64
      if (not subcomponents_only)
        local fixed_x = content_size.x
        if (fixed_x) return fixed_x
      endIf
      if (not first_child) return 0

      local max_width = 0
      forEachChild (component,this)
        local w = component.maximum_width
        if (w == 0) return 0
        max_width += w
      endForEachChild (component)
      return max_width

    method maximum_height( &subcomponents_only )->Real64
      if (not subcomponents_only)
        local fixed_y = content_size.y
        if (fixed_y) return fixed_y
      endIf
      if (not first_child) return 0

      local max_height = 0
      forEachChild (component,this)
        local h = component.maximum_height
        if (h == 0) return 0
        max_height = max_height.or_larger( h )
      endForEachChild (component)
      return max_height

    method minimum_width( &subcomponents_only )->Real64
      if (not subcomponents_only)
        local fixed_x = content_size.x
        if (fixed_x) return fixed_x
      endIf
      if (not first_child) return 0

      local min_width = 0
      forEachChild (component,this)
        min_width += component.minimum_width
      endForEachChild (component)
      return min_width

    method minimum_height( &subcomponents_only )->Real64
      if (not subcomponents_only)
        local fixed_y = content_size.y
        if (fixed_y) return fixed_y
      endIf
      if (not first_child) return 0

      local min_height = 0
      forEachChild (component,this)
        min_height = min_height.or_larger( component.minimum_height )
      endForEachChild (component)
      return min_height

    method operator+( right_component:UIComponent )->UIComponent
      add( right_component )
      return this

    method operator/( bottom_component:UIComponent )->UIComponent
      return UILayout.v.[ add(this), add(bottom_component) ]

    method update_subcomponent_layout
      # Horizontal
      local content_width = minimum_width( &subcomponents_only )
      local max_width = maximum_width

      local min_width = this.size.x
      if (min_width < content_width) min_width = content_width
      if (max_width and min_width > max_width) min_width = max_width

      if (min_width > content_width)
        # Distribute extra width (beyond minimum) to components before laying out
        local extra = min_width - content_width
        local total_flex = this.horizontal_flex
        forEachChild (component,this)
          local flex = component.horizontal_flex
          if (flex)
            local w = Int32( extra * flex / total_flex )
            total_flex -= flex
            component.@size = XY(component.minimum_width+w,0)
            extra -= w
          else
            component.@size = component.minimum_size
          endIf
        endForEachChild (component)

      else
        # All components get set to their minimum width
        forEachChild (component,this)
          component.@size = component.minimum_size
        endForEachChild (component)
      endIf

      local x = @position.x
      forEachChild (component,this)
        local min_height = component.minimum_height
        local max_height = component.maximum_height
        local content_height = this.size.y
        if (content_height < min_height) content_height = min_height
        if (max_height and content_height > max_height) content_height = max_height
        local y = @position.y
        local w = component.size.x
        component.update_layout( Box(x,y,w,this.size.y).subset(XY(w,content_height), component.anchor)+offset )
        x += w
      endForEachChild (component)

    method vertical_flex->Real64
      return which{ minimum_height:0.0 || 1.0 }
endClass
