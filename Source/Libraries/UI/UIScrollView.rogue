module UI

class UIScrollView : UILayout
  PROPERTIES
    offset : XY

  METHODS
    method on( e:ScrollEvent )
      if (Pointer.position and display_bounds.contains(Pointer.position.value))
        offset -= e.delta.yv
        request_redraw
      endIf

    method offset->XY [override]
      return @offset

    method set_offset( @offset )
      @offset = @offset.clamped_high( 0 )
      local bottom_offset = (height - content.height).clamped_high(0)
      if (@offset.y < bottom_offset) @offset = XY( 0, bottom_offset )

    method update_layout_size( container_size:XY ) [override]
      local tracks_bottom = (offset.y == (height - content.height).clamped_high(0))

      prior.update_layout_size( container_size )

      local bottom_offset = (height - content.height).clamped_high(0)
      if (tracks_bottom or offset.y < bottom_offset)
        offset = XY( 0, bottom_offset )
      endIf

endClass

