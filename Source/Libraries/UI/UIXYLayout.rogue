module UI

class UIXYLayout : UILayout
  # An absolute layout. Modifies component positions based on their anchors.
  PROPERTIES
    clamp_content : Logical

  METHODS
    method add( component:UIComponent )
      component.original_position = component.position - this.position # relative to this layout
      prior.add( component )

    method add( component:UIComponent, position:XY, anchor=null:Anchor? )
      if (anchor) component.anchor = anchor.value
      component.original_position = position - this.position # relative to this layout
      prior.add( component )

    method clamp_content
      @clamp_content = true

    method update_subcomponent_bounds( component:UIComponent, bounds:Box )
      bounds = bounds.relative_to( component.anchor ).floor
      if (@clamp_content) bounds = bounds.clamped( this.bounds )
      component.position = bounds.position.floor

    method update_subcomponent_layout [override]
      forEach (component in children)
        update_subcomponent_bounds( component, Box(component.original_position,component.size) )
      endForEach

    method update_subcomponent_sizes( container_size:XY )->XY [override]
      forEach (component in children)
        component.update_layout_size( component.size )
      endForEach

      return container_size
endClass
