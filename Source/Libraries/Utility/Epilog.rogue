class Epilog : BufferedPrintWriter<<buffer>> [singleton]
  # Epilog is a PrintWriter log that stores all output in memory or in a file,
  # optionally echoing to standard output. If the output is not stored to
  # a file then all collected output is printed on program termination
  # (including CTRL+C but not including an uncaught exception).
  #
  # USAGE
  #   uses Utility/Epilog
  #   ...
  #   Epilog.configure( File("Log.txt"), &echo )  # optional
  #   ...
  #   Epilog.println "XYZ"
  #   ...
  PROPERTIES
    echo        : Logical
    writer      : PrintWriter
    history     = StringBuilder()
    is_finished : Logical
 
  METHODS
    method init
      on_exit( this=>on_finish )

    method configure( file=null:File, echo=false )
      if (writer) writer.close; writer = null
      if (file)
        if (file.exists) file.delete
        writer = PrintWriter( file.writer )
      endIf

    method on_finish
      if (is_finished) return
      is_finished = true

      if (writer) writer.close

      if (history.count)
        Global.println  # Get clear of e.g. ^C character
        Global.println history
      endIf

    method flush( buffer:StringBuilder )
      local text = buffer->String
      buffer.clear

      if (writer) writer.[ print text, flush ]
      else        history.print text

      if (echo) Global.[ print text, flush ]
endClass

