class Cmd
  PROPERTIES
    t : Token

  METHODS
    method write( writer:ETCWriter )
      throw t.error( "[INTERNAL] $::write(ETCWriter) is undefined." (type_name) )
endClass

class CmdStatementList
  PROPERTIES
    statements = Cmd[]

  METHODS
    method add( statement:Cmd )->this
      statements.add( statement )
      return this

    method write( writer:ETCWriter )
      writer.write_integer_x( statements.count )
      forEach (statement in statements)
        statement.write( writer )
      endForEach
endClass

class CmdLiteralInteger : Cmd
  PROPERTIES
    value : Integer

  METHODS
    method init( t, value )

    method write( writer:ETCWriter )
      writer.write_integer_x( CmdType.LITERAL_INTEGER )
      writer.write_integer_x( value )
endClass

class CmdLiteralString : Cmd
  PROPERTIES
    value : String

  METHODS
    method init( t, value )

    method write( writer:ETCWriter )
      writer.write_integer_x( CmdType.LITERAL_STRING )
      writer.write_integer_x( value.count )
      forEach (ch in value)
        writer.write_integer_x( ch )
      endForEach
endClass

class CmdLog : Cmd
  PROPERTIES
    operand : Cmd

  METHODS
    method init( t, operand )

    method write( writer:ETCWriter )
      writer.write_integer_x( CmdType.LOG )
      if (operand) operand.write( writer )
endClass

class CmdBinaryOp : Cmd
  PROPERTIES
    left  : Cmd
    right : Cmd

  METHODS
    method init( t, left, right )
endClass

class CmdAdd : CmdBinaryOp
  METHODS
    method write( writer:ETCWriter )
      writer.write_integer_x( CmdType.ADD )
      left.write( writer )
      right.write( writer )
endClass

