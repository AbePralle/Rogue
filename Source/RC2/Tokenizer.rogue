$include "Cmd.rogue"

class Tokenizer
  PROPERTIES
    filepath   : String
    reader     : ParseReader
    tokens     : Token[]
    line       : Integer
    column     : Integer
    buffer     = StringBuilder()
    keywords   = Table<<String,Integer>>()
  
  METHODS
    method init( filepath )
      reader = ParseReader( filepath )
      configure_keywords

    method tokenize->Token[]
      tokens = Token[]
      while (tokenize_another)
        noAction
      endWhile

      add( Token(TokenType.EOI) )

      return tokens

    method configure_keywords
      keywords[ "log" ] = TokenType.KEYWORD_LOG

    method add( t:Token )
      t.filepath = filepath
      t.line = line
      t.column = column
      tokens.add( t )

    method consume( ch:Character )->Logical
      return reader.consume( ch )

    method describe( ch:Character )->String
      if (ch == 0)  return "end of input"
      if (ch == 10) return "end of line"
      return "'$'" (ch)

    method must_consume( ch:Character, message=null:String )
      if (consume(ch)) return

      if (not message)
        message = "Expected $, found $." (describe(ch),describe(reader.peek))
      endIf

      throw RogueError( message, filepath, reader.line, reader.column )

    method consume_whitespace
      reader.consume_spaces

    method tokenize_another->Logical
      consume_whitespace
      if (not reader.has_another) return false

      line = reader.line
      column = reader.column

      local ch = reader.peek

      if (ch == '\n')
        reader.read
        add( Token(TokenType.EOL) )
        return true
      endIf

      if ((ch>='a' and ch<='z') or (ch>='A' and ch<='Z') or (ch=='_'))
        tokenize_identifier
        return true
      endIf

      if (ch >= '0' and ch <= '9')
        tokenize_number
        return true
      endIf

      if (ch == '"')
        tokenize_string
        return true
      endIf

      tokenize_symbol
      return true

    method tokenize_identifier
      buffer.clear

      local ch = reader.peek
      while ((ch>='a' and ch<='z') or (ch>='A' and ch<='Z') or
             (ch=='_') or (ch>='0' and ch<='9'))
        buffer.print( reader.read )
        ch = reader.peek
      endWhile
      
      local id = buffer->String
      local entry = keywords.find( id )
      if (entry)
        add( Token(entry.value) )
      else
        add( IdentifierToken(id) )
      endIf

    method tokenize_number
      local is_real = false
      local whole = tokenize_integer
      local fraction = 0.0
      local exponent = 0.0

      local ch1 = reader.peek
      local ch2 = reader.peek(1)

      if (ch1 == '.' and (ch2>='0' and ch2<='9'))
        is_real = true
        reader.read
        fraction = tokenize_integer
        while (fraction >= 1)
          fraction /= 10
        endWhile
      endIf

      if (consume('e') or consume('E'))
        is_real = true
        local is_negative = false
        if (consume('-')) is_negative = true
        else               consume( '+' )
        exponent = tokenize_integer
        if (is_negative) exponent = -exponent
      endIf

      if (is_real)
        add( LiteralRealToken( (whole+fraction)*10^exponent ) )
      else
        add( LiteralIntegerToken(whole) )
      endIf

    method tokenize_integer->Real
      local result = 0.0
      local ch = reader.peek
      while (ch >= '0' and ch <= '9')
        result = result * 10 + (reader.read - '0')
        ch = reader.peek
      endWhile
      return result

    method tokenize_string
      buffer.clear
      consume( '"' )

      while (reader.has_another)
        local ch = reader.peek
        if (ch == 0 or ch == '\n' or ch == '"') escapeWhile
        buffer.print( reader.read )
      endWhile

      must_consume( '"' )

      add( LiteralStringToken(buffer->String) )

    method tokenize_symbol
      local ch = reader.read

      local type = -1
      which (ch)
        case '+'
          type = TokenType.SYMBOL_PLUS
      endWhich

      if (type != -1)
        add( Token(type) )
      else
        throw RogueError( "Unexpected character '$'." (ch), filepath, line, column )
      endIf

endClass

