class Attribute
  ENUMERATE
    is_class            = 0
    is_aspect           = 1
    is_primitive        = 2
    is_compound         = 3
    type_mask           = 7

    is_direct           = 2
    # AKA by-value type; not a reference type

    is_api              = 16
    is_native           = 32
    is_macro            = 64
    is_initializer      = 128
    is_immutable        = 256
    is_global           = 512
    is_singleton        = 1024
    is_incorporated     = 2048
    is_generated        = 4096
    is_requisite        = 8192
    is_task             = 16384
    is_task_conversion  = 32768
    is_augment          = 65536
    is_abstract         = 131072
    is_routine          = 262144
    is_fallback         = 524288
    is_special          = 1048576
    is_propagated       = 2097152
    is_dynamic          = 4194304
    returns_this        = 8388608
endClass


class Attributes
  PROPERTIES
    flags : Int32
    tags  : String[]

  METHODS
    method init

    method init( flags )

    method cloned->Attributes
      local result = Attributes( flags )
      if (tags)
        forEach (tag in tags)
          result.add( tag )
        endForEach
      endIf
      return result

    method add( flag:Int32 )->Attributes
      flags |= flag
      return this

    method add( tag:String )->Attributes
      if (tags is null) tags = String[]
      tags.add( tag )
      return this

    method add( other:Attributes )->Attributes
      flags |= other.flags
      if (other.tags)
        forEach (tag in other.tags) add( tag )
      endIf
      return this

    method element_type_name->String
      which (flags & Attribute.type_mask)
        case Attribute.is_primitive: return "primitive"
        case Attribute.is_aspect:    return "aspect"
        case Attribute.is_compound:  return "compound"
        others:                      return "class"
      endWhich

endClass
