$include "Standard/Boss.rogue"
$using Boss

class Robot
  GLOBAL PROPERTIES
    phrases = String[] ...
    [
      "I AM ROBOT",
      "ROBOT LOVES KITTEN",
      "ALL YOUR BASE ARE BELONG TO ROBOT"
    ]

  PROPERTIES
    next_phrase = 0

  METHODS
    method init
      println "Creating robot"

    method speak
      println phrases[ next_phrase ]
      ++next_phrase
      next_phrase %= phrases.count
endClass

local vm = VM()

vm.register( "Robot", 0,
  function(task:Task)
    task.push_object( Robot() )
  endFunction
)

vm.register( "Robot.speak", 0,
  function(task:Task)
    (task.object_arg(0) as Robot).speak
    task.push_this
  endFunction
)

vm.register( "println", 0,
  function(task:Task)
    forEach (i in 1..task.arg_count)
      print task.arg(i)
    endForEach
    println
    task.push( null )
  endFunction
)

#local task = vm.execute( "Robot()" )

vm.load @|global x = 5
         |println x
         |x = x + 1
         |println x

vm.load @|println "x:" + x

vm.execute
